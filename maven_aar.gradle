group = GROUP_ID
version = '1.0.0'
apply plugin: 'signing'
apply plugin: 'scala'
apply plugin: 'maven-publish'
sourceCompatibility = 1.8
task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}
task javadocJar(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'scala-filebeat'
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = 'scala-filebeat'
                description = 'scala version filebeat'
                url = 'https://github.com/dounine/scala-filebeat'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'android9527'
                        name = 'nulibj'
                        email = 'nulibj@163.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/dounine/scala-filebeat.git'
                    developerConnection = 'scm:git:ssh://github.com/dounine/scala-filebeat.git'
                    url = 'https://github.com/dounine/scala-filebeat'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username nexusUsername
                password nexusPassword
            }
        }
    }
}
jar {
    manifest {
        attributes 'Implementation-Title': 'ScalaFilebeat',
                'Implementation-Version': version
    }
}
signing {
    sign publishing.publications.mavenJava
}
repositories {
    mavenLocal()
    mavenCentral()
}
dependencies {
    implementation 'org.scala-lang:scala-library:2.11.12'
    implementation group: 'commons-io', name: 'commons-io', version: '2.6'
}